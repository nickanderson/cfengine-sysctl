bundle agent inventory_kernel_settings_sysctl_conf
# @brief Inventory configuration settings found in /etc/sysctl.conf
# Note: This is for the configured settings, not the currently active settings.
{
  vars:
      "_conf_file" string => "/etc/sysctl.conf";
      "_d"
        data => readenvfile( $(_conf_file), inf ),
        ifvarclass => fileexists( $(_conf_file) );
      "_d_i" slist => getindices( @(_d) );

      "sysctl[$(_d_i)]"
        string => "$(_d[$(_d_i)])",
        meta => { "inventory", "attribute_name=/etc/sysctl.conf $(_d_i)" },
        comment => concat( "Inventory each configured sysctl value so that we can",
                           " report on it and view differences across the environment",
                           " before we make policy changes.");
}

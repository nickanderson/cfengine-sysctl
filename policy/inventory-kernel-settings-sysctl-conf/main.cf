bundle agent inventory_kernel_settings_sysctl_conf
# @brief Inventory configuration settings found in sysctl.conf
# Note: This is for the configured settings, not the currently active settings.
{
  vars:
      "sysctl_conf" string => "/etc/sysctl.conf";
      # Parse the file into a data structure
      "sysctl_conf_data"
        data => data_readstringarray($(sysctl_conf),
                                     "\s*#[^\n]*",
                                     "\s?+=\s?+",
                                     inf,
                                     inf),
        ifvarclass => fileexists( $(sysctl_conf) );

      "sysctl_keys" slist => getindices("sysctl_conf_data");

      # We have to iterate through each key value pair and create
      # cfengine variables so that we can tag them for inventory
      "sysctl[$(sysctl_keys)]" -> { "Mission Portal", "CFEngine Enterprise" }
        string => "$(sysctl_conf_data[$(sysctl_keys)])",
        meta => { "inventory", "attribute_name=Sysctl Config $(sysctl_keys)" },
        comment => "Inventory each configured sysctl value so that we can
                    report on it and view differences across the environment
                    before we make policy changes.";
}
